/*This code is provided for the study "Outcomes of SARS-CoV-2 Reinfection"
This code is part of an ongoing project and should be considering a work in progress

This code was last revised 08/03/2022

The analyses conducted here provide a comparison of those with a reinfection to the non-reinfected.

For the macro reinfection the input and relevant parameters are:
id: id variable for the participant
outcome: the name of the outcome stored as a date. If the participant has no outcome, this should be missing
history: the name of the history variable that stores prior history of the outcome
	in question. A 1 or date may be used to indicate a prior history, while a 0 or missing will indicate no history.
hr: stores hazard ratio estimates
sur: stores survival estimates

hdcov: high dimensional covariates
class: categorical prespecified, non-high dimensional covariates
continuous: continuous prespecified, non-high dimensional covariates

cohort: dataset containing the cohort information, covariates (both prespecified and high dimensional), history, and outcome variables.
t1: Start of follow-up for the participant
endoffollow: end of follow-up for the participant
group: Indicator of group membership. Values are "REIN" for the reinfection and "NONI" for the non-reinfected

target: dataset containing the target population for weighting and their relevant covariate information and history

*/

%macro reinfection (id, outcome, history, hr, sur, hdcov, class, cont, cohort, target);

data &hr;
set _NULL_;
run;

data &sur;
set _NULL_;
run;

data store;
set _NULL_;
run;

data target;
set &target;
if &history<=0;
group="TARG";
run;

data covid;
set &cohort;
if &history<=0;
run;

/*Develop the propensity scores*/
data combine;
set covid target;
run;

data model;
set combine;
if group="NONI" then delete;
if group="TARG" then classbreak=0; else classbreak=1;
run;

proc logistic data=model noprint;
class &class;
model classbreak(event="1")=
&class &cont &hdcov;
output out=ps_RE (keep=&id ps_RE group) pred=ps_RE ;
run;

data model;
set combine;
if group="REIN" then delete;
if group="TARG" then classbreak=0; else classbreak=1;
run;

proc logistic data=model noprint;
class &class ;
model classbreak(event="1")=
&class &cont &hdcov;
output out=ps_NO (keep=&id ps_NO group) pred=ps_NO ;
run;

proc sort data=covid; by &id; run;
proc sort data=ps_RE; by &id; run;
proc sort data=ps_NO; by &id; run;

/*Weights groups to the target population*/
data ps ;
merge covid (in=a keep=&id &outcome t1 endoffollow)
ps_RE (where=(group="REIN")) 
ps_NO (where=(group="NONI"));
by &id;
if a;
if group="REIN" then wt=(1-ps_RE)/ps_RE;
if group="NONI" then wt=(1-ps_NO)/ps_NO;
run;

data outcome;
set ps;
if &outcome ^=. and &outcome<=endoffollow then do;
event=1;
time=&outcome - t1;
end;
else do;
event=0;
time=endoffollow - t1;
end;
run;

/* for survival estimation for both groups at 180 days from t1*/
data baseline;
length group $16.;
event=0; time=180;
group="REIN";output;
group="NONI"; output;
run;

ods output hazardratios=hr;
proc phreg data=outcome covs (aggregate) ;
class group (ref="NONI");
model time*event(0)=group;
hazardratio group/diff=pairwise;
weight wt;
id &id;
baseline out=sur covariates=baseline survival=sur timelist=180;
run;

data &hr;
set &hr hr (in=a);
if a then do; outcome="&outcome"; end;
run;

data &sur;
set &sur sur (in=a);
if a then do;  type="POST"; outcome="&outcome"; end;
run;

%mend;
